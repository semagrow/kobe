PREFIX bibo: <http://purl.org/ontology/bibo/>
PREFIX chembl: <http://rdf.ebi.ac.uk/terms/chembl#>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX wp: <http://vocabularies.wikipathways.org/wp#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
SELECT *
WHERE {
    VALUES ?pw_uri { <http://identifiers.org/wikipathways/WP179> }.
    GRAPH <http://www.wikipathways.org> {
        ?rev dc:identifier ?pw_uri .
        ?rev dc:title ?title .
        ?gene_product_internal dcterms:isPartOf ?rev .
        ?gene_product_internal rdf:type ?type .
        ?gene_product_internal dc:identifier ?gene_product .
        FILTER ( ?type = wp:GeneProduct || ?type = wp:Protein ) .
    }
    GRAPH <http://IMS> {
        ?gene_product skos:exactMatch ?item .
    }
    GRAPH <http://www.ebi.ac.uk/chembl> {
        ?assay_uri chembl:hasTarget ?chembl_target_uri .
        ?assay_uri chembl:hasActivity ?item .
        ?chembl_target_uri dcterms:title ?target_name_chembl .
        ?chembl_target_uri chembl:organismName ?target_organism .
        ?item chembl:standardUnits <http://www.openphacts.org/units/Nanomolar> .
        ?item chembl:standardType ?activity_type .
        ?activity_type rdfs:label "Potency" .
        ?item chembl:standardValue ?standard_value .
        BIND (xsd:decimal(?standard_value) AS ?activity_value) .
        FILTER (?activity_value <= 1000) .
        ?item chembl:hasDocument ?doc_uri .
        ?doc_uri bibo:pmid ?pmid .
    }
}
